apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
mainClassName = 'com.twitter.zipkin.query.Main'

run {
    // When configKey == "dev", run out of the collector's directory to reuse zipkin.db
    workingDir configKey == "dev" ? project(':zipkin-collector-service').buildDir : project.buildDir
    // See gradle.properties for an explanation of configKey
    args '-f', "${projectDir}/config/query-${configKey}.scala"
}

tasks.build.dependsOn(shadowJar)

repositories {
    // For dependencies of zipkin-cassandra
    maven { url 'http://conjars.org/repo' }
    // For zipkin-hbase
    maven { url 'https://repository.cloudera.com/content/groups/cdh-releases-rcs/' }
}

dependencies {
    compile project(':zipkin-query-core')
    compile project(':zipkin-cassandra')
    compile project(':zipkin-redis')
    compile project(':zipkin-anormdb')
    compile project(':zipkin-hbase')
}

sourceSets.test.resources {
    srcDir 'config'
}
